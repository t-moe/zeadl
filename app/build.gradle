apply plugin: 'com.android.application'

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    defaultConfig {
        applicationId "ch.bfh.android.zeadl"
        minSdkVersion 16
        targetSdkVersion 21
        versionCode 1
        versionName "1.0"
    }

    productFlavors {
        emulator {
            //no jni
        }
        beaglebone {
            ndk {
                moduleName "i2c"
            }
        }
    }

    sourceSets.main {
        jni.srcDirs = [] //disable automatic ndk-build call
    }

    sourceSets.beaglebone {
        jniLibs.srcDir 'src/main/libs'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}


dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile group: 'org.achartengine', name: 'achartengine', version: '1.2.0'
    compile 'com.android.support:appcompat-v7:21.0.3'
    compile 'com.android.support:support-v4:21.0.3'
    compile project(":color-picker-view")
}

repositories {
    maven {
        url "https://repository-achartengine.forge.cloudbees.com/snapshot/"
    }
}


import org.apache.tools.ant.taskdefs.condition.Os

// Call regular ndk-build(.cmd) script from app directory
task ndkBuild(type: Exec) {
    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        commandLine 'ndk-build.cmd', '-C', file('src/main').absolutePath
    } else {
        commandLine 'ndk-build', '-C', file('src/main').absolutePath
    }
}

tasks.withType(JavaCompile) {
    compileTask -> compileTask.dependsOn ndkBuild
}


